buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowJarVersion"
        classpath "gradle.plugin.org.openrepose:gradle-linkchecker-plugin:$gradleLinkcheckerPluginVersion"
        classpath "gradle.plugin.org.aim42:htmlSanityCheck:$htmlSanityCheckVersion"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: "org.openrepose.gradle.plugins.linkchecker"
apply plugin: 'org.aim42.htmlSanityCheck'

ext {
    siteDir = 'site'
}

repositories {
    jcenter()
}

mainClassName = "org.grails.guides.Main"

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
    compile project(':grails-navigation-core')
}

task createSiteFolder {
    doLast {
        file("${project.buildDir}/${siteDir}").mkdirs()
        file("${project.buildDir}/${siteDir}/tags").mkdirs()
        file("${project.buildDir}/${siteDir}/categories").mkdirs()
    }
}

task copyAssets {
    doLast {
        copy {
            from "${rootProject.projectDir}/plugin/grails-app/assets/images"
            into "${project.buildDir}/${siteDir}/images"
            include '*.svg'
            include '*.png'
            include '*.svg'
            include '*.ico'
        }
        copy {
            from "${rootProject.projectDir}/plugin/grails-app/assets/stylesheets"
            into "${project.buildDir}/${siteDir}/stylesheets"
            include '*.css'
        }
        copy {
            from "${rootProject.projectDir}/plugin/grails-app/assets/javascripts"
            into "${project.buildDir}/${siteDir}/javascripts"
            include '*.js'
        }
        copy {
            from "${rootProject.projectDir}/plugin/grails-app/assets/fonts"
            into "${project.buildDir}/${siteDir}/fonts"
            include '*.eot'
            include '*.svg'
            include '*.ttf'
            include '*.woff'
            include '*.woff2'
        }
    }
}

copyAssets.dependsOn = ['createSiteFolder']

processResources.dependsOn = ['copyAssets']

linkchecker {
    inputDir = new File("${project.buildDir}/${siteDir}")
}
runShadow.finalizedBy 'linkchecker'

htmlSanityCheck {
    sourceDir = new File("${project.buildDir}/${siteDir}")
}
runShadow.finalizedBy(htmlSanityCheck)