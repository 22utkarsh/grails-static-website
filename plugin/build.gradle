buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$buildInfoExtractorGradleVersion"
    }
}

version projectVersion
group "org.grails.plugins"

apply plugin: 'idea'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.grails-plugin-publish'
apply plugin: 'asset-pipeline'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'com.jfrog.artifactory'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.grails:grails-core"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    profile "org.grails.profiles:web-plugin"
    compile "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    compile "org.grails.plugins:gsp"
    compile project(':grails-navigation-core')
}

bootRun {
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}
// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false
grailsPublish {
    user = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : ''
    key = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : ''
    userOrg = project.hasProperty('userOrg') ? project.userOrg : 'grails'
    repo = project.hasProperty('repo') ? project.repo : 'plugins'
    websiteUrl = project.hasProperty('websiteUrl') ? project.websiteUrl : "https://github.com/grails/grails-static-website"
    license {
        name = project.hasProperty('license') ? project.license : 'Apache-2.0'
    }
    issueTrackerUrl = project.hasProperty('issueTrackerUrl') ? project.issueTrackerUrl : "https://github.com/grails-plugins/$project.name/issues"
    vcsUrl = project.hasProperty('vcsUrl') ? project.vcsUrl : "https://github.com/grails-plugins/$project.name"
    title = 'Grails Navigation'
    desc = 'Encapsulate style and navigation of Grails redesign'
    developers = [sdelamo:"Sergio del Amo"]
}


assets {
    packagePlugin = true
    includes = ["fonts/*"]
}
